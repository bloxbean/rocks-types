plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'org.ajoberstar.grgit' version '5.2.0'
}

repositories {
    mavenCentral()
    mavenLocal()
}

compileJava {
    sourceCompatibility = '21'
    targetCompatibility = '21'

    options.compilerArgs += ["-Aproject=${project.group}/${project.name}"]
}


artifacts {
    archives jar
}

dependencies {
    api project(':core')
}

test {
    useJUnitPlatform()
}

// Make all tests use JUnit 5
tasks.withType(Test) {
    useJUnitPlatform()
}

def commit_id=getCheckedOutGitCommitHash()
if (project.version.endsWith("-SNAPSHOT")) {
    version = "${project.version}".replace("-SNAPSHOT", "-${commit_id}-SNAPSHOT")
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'rocks-types'
                description = 'Rocks-Types: Data Type Implementations Built on RocksDB'
                url = 'https://github.com/bloxbean/rocks-types'
                licenses {
                    license {
                        name = 'The MIT License'
                        url = 'https://opensource.org/licenses/mit-license.php'
                    }
                }
                developers {
                    developer {
                        id = 'satran004'
                        name = 'Satya'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/bloxbean/rocks-types.git'
                    developerConnection = 'scm:git:ssh://git@github.com/bloxbean/rocks-types.git'
                    url = 'https://github.com/bloxbean/rocks-types'
                }
            }
        }
    }

    repositories {
        String ossrhUsername = System.getenv('MAVEN_USERNAME')
        String ossrhPassword = System.getenv('MAVEN_PASSWORD')

        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

if (isReleaseVersion && !project.hasProperty("skipSigning")) {
    signing {
        sign publishing.publications
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots"
        }
        maven { url 'https://jitpack.io' }
    }

    archivesBaseName = 'rocks-types-' + project.name
    if (project.version.endsWith("-SNAPSHOT")) {
        version = "${project.version}".replace("-SNAPSHOT", "-${commit_id}-SNAPSHOT")
    }

    dependencies {
        compileOnly libs.lombok
        annotationProcessor libs.lombok

        testImplementation libs.junit.jupiter.api
        testImplementation libs.junit.jupiter.params
        testRuntimeOnly libs.junit.jupiter.engine
        testImplementation('org.assertj:assertj-core:3.23.1')
        testRuntimeOnly libs.slf4j.log4j

        testCompileOnly libs.lombok
        testAnnotationProcessor libs.lombok
    }

    compileJava {
        sourceCompatibility = '21'
        targetCompatibility = '21'

        options.compilerArgs += ["-Aproject=${project.group}/${project.name}"]
    }

    tasks.withType(Javadoc) {
        options.encoding = 'UTF-8'
    }

    task sourceJar(type: Jar) {
        archiveClassifier.set("sources")
        from sourceSets.main.allJava
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        archiveClassifier.set("javadoc")
        from javadoc.destinationDir
    }

    artifacts {
        archives jar
        archives sourceJar
        archives javadocJar
    }

    // Make all tests use JUnit 5
    tasks.withType(Test) {
        useJUnitPlatform()
    }

    sourceSets {
        integrationTest {
            compileClasspath += sourceSets.main.output
            runtimeClasspath += sourceSets.main.output
        }
    }

    configurations {
        integrationTestImplementation.extendsFrom testImplementation
        integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
        integrationTestCompileOnly.extendsFrom testCompileOnly
        integrationTestAnnotationProcessor.extendsFrom testAnnotationProcessor
    }

    task integrationTest(type: Test) {
        description = 'Runs the integration tests.'
        group = 'verification'
        testClassesDirs = sourceSets.integrationTest.output.classesDirs
        classpath = sourceSets.integrationTest.runtimeClasspath
    }


    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact(sourceJar)
                artifact(javadocJar)

                artifactId 'rocks-types-' + project.name
                pom {
                    url = 'https://github.com/bloxbean/rocks-types'
                    licenses {
                        license {
                            name = 'The MIT License'
                            url = 'https://opensource.org/licenses/mit-license.php'
                        }
                    }
                    developers {
                        developer {
                            id = 'satran004'
                            name = 'Satya'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/bloxbean/rocks-types.git'
                        developerConnection = 'scm:git:ssh://git@github.com/bloxbean/rocks-types.git'
                        url = 'https://github.com/bloxbean/rocks-types'
                    }
                }
            }
        }

        repositories {
            String ossrhUsername = System.getenv('MAVEN_USERNAME')
            String ossrhPassword = System.getenv('MAVEN_PASSWORD')

            maven {
                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username ossrhUsername
                    password ossrhPassword
                }
            }
        }
    }

    ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

    if (isReleaseVersion && !project.hasProperty("skipSigning")) {
        signing {
            sign publishing.publications
        }
    }
}

def getCheckedOutGitCommitHash() {
    grgit.head().abbreviatedId
}
